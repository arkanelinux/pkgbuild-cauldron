pkgname=system76-scheduler
pkgver=2.0.1
pkgrel=1
pkgdesc='Auto-configure CFS and process priorities for improved desktop responsiveness'
url='https://github.com/pop-os/system76-scheduler'
source=("https://github.com/pop-os/system76-scheduler/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('6b6d627bbc5350cd436bd88a25ad577bcd6c5198c463162f74e96103f742083c')
arch=('x86_64')
license=('MPL-2.0')
makedepends=('rust' 'just' 'linux-headers' 'pipewire' 'llvm' 'clang')
depends=('bcc-tools' 'python-bcc')

build () {
	cd "$srcdir/$pkgname-$pkgver"

	just execsnoop=/usr/share/bcc/tools/execsnoop build-release
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	just sysconfdir=/usr/share rootdir="$pkgdir" install
}
