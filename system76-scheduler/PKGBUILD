# Maintainer: Dennis ten Hoove <contact@arkanelinux.org>

pkgname=system76-scheduler
pkgver=2.0.1.3.g467617a
pkgrel=1
epoch=2
pkgdesc='system76 userspace scheduler'
arch=(x86_64)
url='https://github.com/pop-os/system76-scheduler'
license=('MPL-2')
provides=(${pkgname})
conflicts=(${pkgname})
depends=(bcc-tools python-bcc)
makedepends=(git rust just linux-headers pipewire)
source=("git+$url")
sha256sums=('SKIP')
backup=(
  'etc/system76-scheduler/assignments/default.ron'
)

pkgver() {
  cd ${pkgname}
  git describe --tags | sed -e "s/^v//" | tr '-' '.'
}

build() {
  cd ${pkgname}
  just execsnoop=/usr/share/bcc/tools/execsnoop build-release
}

package() {
  cd ${pkgname}
  just sysconfdir=/usr/share rootdir=${pkgdir} install
}
